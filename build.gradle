apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'signing'

description = 'SharePoint Library'
group = 'io.github.leeds-beckett-digital-learning'
version='0.1.0.SNAPSHOT'


/** More detailed warnings from compiler. **/
compileJava {
    options.compilerArgs << '-Xlint:unchecked' 
}

/** Make sure code runs on Java 11 even though compiler is more recent. **/
java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}


repositories {
    mavenCentral()
}


dependencies {
    implementation 'commons-logging:commons-logging:1.3.5'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.reflections:reflections:0.10.2'
}

run {   
    systemProperties['org.apache.commons.logging.Log'] = 'org.apache.commons.logging.impl.SimpleLog'
    systemProperties['org.apache.commons.logging.simplelog.log.org.apache.http.wire'] = 'ERROR'
}



signing {
    sign(publishing.publications)
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
      pom {
          name = rootProject.name
          description = 'A Java library to support applications that access Sharepoint via OData v3'
          url = 'https://github.com/leeds-beckett-digital-learning/jesharepoint'
          licenses {
              license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              }
          }
          developers {
              developer {
                  id = 'maber01'
                  name = 'Jon Maber'
                  email = 'j.r.maber@leedsbeckett.ac.uk'
              }
          }
          scm {
              connection = 'scm:git:https://github.com/leeds-beckett-digital-learning/jesharepoint.git'
              developerConnection = 'scm:git:git@github.com:leeds-beckett-digital-learning/jesharepoint.git'
              url = 'https://github.com/leeds-beckett-digital-learning/jesharepoint'
          }
      }
    }
  }
  repositories {
    maven {
      name = "OSSRH"
      url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUserName
        password = ossrhToken
      }
    }
  }
}
